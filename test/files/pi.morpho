;;;  Eftirfarandi forrit reiknar nálgun á pí með heiltölureikningum.
;;;  Það byggist á formúlunni:
;;;      pí = 4(4arctan(1/5)-arctan(1/239))
;;;  Þetta er nokkuð hraðvirk aðferð til að reikna nálgun á pí.

;;;  Höfundur: Snorri Agnarsson

"pi.mexe" = main in
!
{{
main =
	fun()
	{
		try
		{
			main2();
		}
		catch(e)
		{
			printExceptionTrace(e);
		};
	};

main2 =
  fun()
	{
		var h,n,pí;
		;;;  Reiknum heiltölunálgun á 4(arctan(1/5)-arctan(1/239))10^(n+6)

		n = "java.lang.Integer".##parseInt(("java.lang.String")getArgs()[1]);

		h = bigInteger(10)^(n+6);
		pí = 4*(4*arctanrev(h,5)-arctanrev(h,239));

		;;;  Skrifum niðurstöðuna, en sleppum sex síðustu stöfunum:
		write("pí = 3.");
		writeln(((pí-3*h)/1000/100+5)/10);
		exit(0);
	};

;;;  Stefið arctanrev hefur eftirfarandi lýsingu:
;;;  Notkun:
;;;    a = arctanrev(h,x)
;;;  Fyrir:
;;;    h er heiltala.
;;;    x er heiltala.
;;;  Eftir:
;;;    a inniheldur heiltölu sem er nálgun á h*arctan(1/x)
;;;    sem reiknuð er samkvæmt Taylor-röðinni fyrir arctan.
;;;    Taylor-röðina fyrir arctan má finna í ýmsum formúlubókum.
arctanrev =
	fun(h,x)
	{
		var summa,xx,i;
		i = 1;
		xx = x*x;
		summa = h = h/x;
		while( h!=0 )
		{
			i = i+2;
			h = h/xx;
			summa = summa-h/i;
			i = i+2;
			h = h/xx;
			summa = summa+h/i;
		};
		summa;
	};

;;; Notkun: y = x^n
;;; Fyrir:  n er heiltala, n>=0.
;;;         x er heiltala.
;;; Eftir:  y er x í veldinu n
^ =
	fun(x,n)
	{
		var p=1;
		for(;;)
		{
			if( n%2 != 0 ) { p = p*x; };
			n = n/2;
			if( n==0 ) {return p};
			x = x*x;
		}
	};
}}
*
BASIS
;
